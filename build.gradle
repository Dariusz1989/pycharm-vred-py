plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.18'
}

group 'de.chr33z'
version '0.1.1'

def hasPyCharm = project.hasProperty('pycharmPath')
def hasPythonPlugin = project.hasProperty('pythonPlugin')

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    if (hasPyCharm) {
        compileOnly name: 'pycharm'
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    }
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version ideaVersion
    downloadSources Boolean.valueOf(downloadIdeaSources)
    updateSinceUntilBuild = true
    if (hasPyCharm) {
        alternativeIdePath pycharmPath
    }
    if (hasPythonPlugin) {
        plugins += [pythonPlugin]
    }
}

patchPluginXml {
    version project.property('version')
    sinceBuild project.property('sinceBuild')
    untilBuild project.property('untilBuild')
}